#!/usr/bin/env zsh

##
# tms - User-friendly wrapper for transmission-cli
#
# USAGE
#   Run this program without arguments to see list of options.
#
# @author Kutsan Kaplan <me@kutsankaplan.com>
# @license GPLv3
# @version v0.1.0
##

setopt ERR_EXIT NO_UNSET PIPE_FAIL WARN_CREATE_GLOBAL WARN_NESTED_VAR

typeset -g VERSION='v0.1.0'

##
# Show program usage informations and exit.
##
function show_help() {
cat << EOF

  ${bold_color}tms${reset_color} - Tranmission CLI Wrapper

  ${bold_color}SYNOPSIS${reset_color}
    $ ${fg[green]}tms${reset_color} <create|daemon|edit|remote|show> <subcommand args>

  ${bold_color}AUTHOR${reset_color}
    Kutsan Kaplan <me@kutsankaplan.com>

  ${bold_color}LICENSE${reset_color}
    GPLv3

  ${bold_color}VERSION${reset_color}
    $VERSION

EOF

	exit 0
}

##
# @param {string} $1 Subcommand.
# @param {string} $2 Subcommand arguments.
##
function main() {
	source $ZDOTDIR/lib/console.zsh 2>/dev/null || {
		echo
		echo -e '  \033[41m error \033[0m This script requires \033[01mconsole.zsh\033[0m component to provide sane error output.'
		echo "          There was nothing found at \033[01m$ZDOTDIR/lib/console.zsh\033[0m."
		echo

		exit 1
	}

	if (! (hash transmission-remote && hash transmission-daemon) 2>/dev/null) {
		echo; console.error "${bold_color}transmission-cli is not installed.${reset_color}"; echo

		exit 1
	}

	local subcommand=${1:-''}

	case "$subcommand" {
		'-h' | '--help' | 'help')
			show_help
			;;

		'--version' | 'version')
			echo; console.info "tms $VERSION"; echo
			;;

		'daemon')
			shift

			if [[ "$@" == '' ]] {
				if ! (pgrep -x 'transmission-daemon' &>/dev/null) {
					eval transmission-daemon $TRANSMISSION_DAEMON_OPTIONS
				} else {
					echo; console.warn 'transmission-daemon is already running.'; echo
				}
			} else {
				transmission-daemon "$@"
			}
			;;

		'remote')
			shift
			eval transmission-remote $TRANSMISSION_REMOTE_OPTIONS "$@"
			;;

		'create' | 'edit' | 'show')
			shift
			transmission-${subcommand} "$@"
			;;

		*)
			transmission-remote "$@"
			;;
	}
}
main "$@"
